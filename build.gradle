version = '1.0.0'  // Last released version
//version '1.0.1-SNAPSHOT'
group 'com.xebialabs.restito'

defaultTasks 'build'

apply plugin: 'java-library'
apply plugin: 'scala'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'idea'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	testCompileOnly 'org.scala-lang:scala-library:2.12.16'

	testImplementation 'org.slf4j:slf4j-simple:1.7.21'
	testImplementation 'org.mockito:mockito-core:1.10.19'
	testImplementation 'org.hamcrest:hamcrest-all:1.3'
	testImplementation('io.rest-assured:rest-assured:3.1.1')
	testImplementation"com.typesafe.akka:akka-actor_2.12:2.5.17"
	testImplementation"com.typesafe.akka:akka-stream_2.12:2.5.17"
	testImplementation"com.typesafe.akka:akka-http_2.12:10.1.5"
	testImplementation"org.scalatest:scalatest_2.12:3.0.5"

	implementation 'junit:junit:4.13.2'
	implementation 'org.slf4j:slf4j-api:1.7.21'
	implementation 'org.glassfish.grizzly:grizzly-http-server:2.3.25'
	implementation 'com.jayway.jsonpath:json-path:2.1.0'
	implementation 'org.apache.mina:mina-core:2.1.6'
}

tasks.withType(JavaCompile) { compile ->
	compile.options.compilerArgs << '-Xlint:deprecation'
	compile.options.compilerArgs << '-Xlint:unchecked'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

javadoc {
	if(JavaVersion.current() == JavaVersion.VERSION_11) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

if (project.hasProperty("sonatypeUsername")) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = 'restito'
				from components.java

				pom {
					name = 'Restito'
					description = 'Restito - testing framework for rest clients'
					packaging = 'jar'
					url = 'http://mkotsur.github.com/restito'

					licenses {
						license {
							name = 'MIT'
							url = 'https://raw.github.com/mkotsur/restito/master/LICENSE'
						}
					}

					developers {
						developer {
							id = 'mkotsur'
							name = 'Mike Kotsur'
							email = 'miccots@gmail.com'
						}
					}
					scm {
						url = 'scm:git@github.com:mkotsur/restito.git'
						connection = 'scm:git@github.com:mkotsur/restito.git'
						developerConnection = 'scm:git@github.com:mkotsur/restito.git'
					}
				}
			}
		}

		repositories {
			maven {
				url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}

	}

	signing {
		sign publishing.publications.mavenJava
	}
}


//uploadArchives {
//	if (project.hasProperty("sonatypeUsername")) {
//		repositories {
//			mavenDeployer {
//
//				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
//					authentication(userName: sonatypeUsername, password: sonatypePassword)
//				}
//
//

//				}
//			}
//		}
//	}
//}
