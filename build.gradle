// version = '0.5.1'  // Last released version
version '0.6'
group 'com.xebialabs.restito'

defaultTasks 'build'

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'org.scala-lang:scala-library:2.11.5'

    compile 'junit:junit:4.12'

    compile 'org.slf4j:slf4j-api:1.7.5'
    testCompile 'org.slf4j:slf4j-simple:1.7.5'

    compile 'org.mockito:mockito-core:1.10.17'
    compile 'org.glassfish.grizzly:grizzly-http-server:2.3.17'
    compile 'com.sun.jersey:jersey-grizzly2:1.18.3'

	compile 'com.jayway.jsonpath:json-path:2.0.0'

    compile 'org.apache.mina:mina-core:2.0.4'

    testCompile 'org.hamcrest:hamcrest-all:1.3'

    testCompile ('com.jayway.restassured:rest-assured:2.4.0') {
      exclude(module: 'groovy')
    }

    testCompile "io.spray:spray-client_2.11:1.3.2"
    testCompile "com.typesafe.akka:akka-actor_2.11:2.3.9"
    testCompile "org.scalatest:scalatest_2.11:2.2.4"

    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.3.2'
}

tasks.withType(JavaCompile) { compile ->
    compile.options.compilerArgs << '-Xlint:deprecation'
    compile.options.compilerArgs << '-Xlint:unchecked'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
  def username = System.console().readLine("\nEnter sonatype username: ")
  def password = new String(System.console().readPassword("\nEnter sonatype password: "))
  repositories {
      mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: username, password: password)
          }

          pom.project {
              name 'Restito'
              packaging 'jar'
              description 'Restito - testing framework for rest clients'
              url 'http://mkotsur.github.com/restito'

              scm {
                  url 'scm:git@github.com:mkotsur/restito.git'
                  connection 'scm:git@github.com:mkotsur/restito.git'
                  developerConnection 'scm:git@github.com:mkotsur/restito.git'
              }

              licenses {
                  license {
                      name 'MIT'
                      url 'https://raw.github.com/mkotsur/restito/master/LICENSE'
                      distribution 'repo'
                  }
              }

              developers {
                  developer {
                      id 'mkotsur'
                      name 'Mykhailo Kotsur'
                  }
              }
          }
      }
  }

}
